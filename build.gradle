// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "me.tatarka:gradle-retrolambda:3.7.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

ext {
    compileSdk = 27
    targetSdk = 27
    minSdk = 19

    java = '1.7'
    support = '27.1.1'
    playService = '15.0.0'
    viewModel = '1.1.1'
    room = '1.0.0'
    butterKnife = '8.8.1'
    glide = '4.5.0'
    constraintLayout = '1.1.0'
    retrofit = '2.3.0'
    rxAndroid = '2.0.1'
    rxJava = '2.1.7'
    junit = '4.12'
    runner = '1.0.2'
    esspressoCore = '3.0.2'
}

def applications = [project('customerClient'), project('storeClient'), project('storeServer')]
configure(applications) {
    apply plugin: 'com.android.application'
    apply plugin: 'me.tatarka.retrolambda'

    dependencies {
        implementation project(":clientCore")

        implementation "com.android.support:appcompat-v7:$support"
        implementation "com.android.support:design:$support"
        implementation "com.android.support:cardview-v7:$support"
        implementation "com.android.support:recyclerview-v7:$support"

        implementation "android.arch.lifecycle:extensions:$viewModel"
        annotationProcessor "android.arch.lifecycle:compiler:$viewModel"

        implementation "com.jakewharton:butterknife:$butterKnife"
        annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"

        implementation "com.github.bumptech.glide:glide:$glide"
        annotationProcessor "com.github.bumptech.glide:compiler:$glide"

        implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
    }
}

def libraries = [project('clientCore')]
configure(libraries) {
    apply plugin: 'com.android.library'
    apply plugin: 'me.tatarka.retrolambda'

    dependencies {
        implementation "com.squareup.retrofit2:retrofit:$retrofit"
        implementation "com.squareup.retrofit2:converter-gson:$retrofit"
        implementation "com.squareup.retrofit2:adapter-rxjava:$retrofit"

        implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
        implementation "io.reactivex.rxjava2:rxjava:$rxJava"

        implementation "android.arch.persistence.room:runtime:$room"
        implementation "android.arch.persistence.room:rxjava2:$room"
        annotationProcessor "android.arch.persistence.room:compiler:$room"

        api "io.reactivex.rxjava2:rxandroid:$rxAndroid"
        api "io.reactivex.rxjava2:rxjava:$rxJava"

        testApi "junit:junit:$junit"
        androidTestApi "com.android.support.test:runner:$runner"
        androidTestApi "com.android.support.test.espresso:espresso-core:$esspressoCore"

        implementation "com.google.android.gms:play-services-nearby:$playService"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
