// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        jcenter()
        mavenCentral()
    }
}

ext {
    compileSdk = 27
    targetSdk = 27
    minSdk = 19

    java = '1.7'
    support = '27.1.1'
    playService = '15.0.0'
    viewModel = '1.1.1'
    room = '1.1.0'
    butterKnife = '8.8.1'
    glide = '4.7.1'
    constraintLayout = '1.1.0'
    retrofit = '2.3.0'
    rxAndroid = '2.0.1'
    rxJava = '2.1.7'
    junit = '4.12'
    runner = '1.0.2'
    esspressoCore = '3.0.2'
    commonsio = '2.6'
    multidex = '1.0.3'
}

def applications = [project('customerClient'), project('storeClient'), project('storeServer')]
configure(applications) {
    apply plugin: 'com.android.application'

    android {
        lintOptions {
            disable 'LongLogTag', 'CheckResult'
        }

        defaultConfig {
            multiDexEnabled true
        }
    }
    dependencies {
        implementation project(":clientCore")

        implementation "com.android.support:design:$support"
        implementation "com.android.support:cardview-v7:$support"
        implementation "com.android.support:recyclerview-v7:$support"

        implementation "android.arch.lifecycle:extensions:$viewModel"
        annotationProcessor "android.arch.lifecycle:common-java8:$viewModel"

        implementation "com.jakewharton:butterknife:$butterKnife"
        annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"

        implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
        implementation "com.android.support:multidex:$multidex"

        testImplementation "junit:junit:$junit"
        androidTestImplementation "com.android.support.test:runner:$runner"
        androidTestImplementation "com.android.support.test.espresso:espresso-core:$esspressoCore"
    }
}

def libraries = [project('clientCore')]
configure(libraries) {
    apply plugin: 'com.android.library'

    android {
        lintOptions {
            disable 'LongLogTag', 'CheckResult'
        }

        defaultConfig {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
                }
            }
        }
    }
    dependencies {
        api "com.android.support:appcompat-v7:$support"

        implementation "com.squareup.retrofit2:retrofit:$retrofit"
        implementation "com.squareup.retrofit2:converter-gson:$retrofit"
        implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"

        implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
        implementation "io.reactivex.rxjava2:rxjava:$rxJava"

        implementation "android.arch.persistence.room:runtime:$room"
        implementation "android.arch.persistence.room:rxjava2:$room"
        annotationProcessor "android.arch.persistence.room:compiler:$room"
        implementation "android.arch.persistence.room:guava:$room"

        api "io.reactivex.rxjava2:rxandroid:$rxAndroid"
        api "io.reactivex.rxjava2:rxjava:$rxJava"

        testImplementation "junit:junit:$junit"
        androidTestImplementation "com.android.support.test:runner:$runner"
        androidTestImplementation "com.android.support.test.espresso:espresso-core:$esspressoCore"

        implementation "com.google.android.gms:play-services-nearby:$playService"
        implementation "commons-io:commons-io:$commonsio"

        api "com.github.bumptech.glide:glide:$glide"
        annotationProcessor "com.github.bumptech.glide:compiler:$glide"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
