/*
 * Copyright (c) 2018. Mark Soft - All Rights Reserved
 * Unauthorized copying of this file, via any medium is strictly prohibited
 * Proprietary and confidential
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'io.fabric.tools:gradle:1.25.4'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com/' }
        maven { url 'http://devrepo.kakao.com:8088/nexus/content/groups/public/' }
    }
}

ext {
    compileSdk = 28
    targetSdk = 28
    minSdk = 21

    java = '1.7'
    support = '27.1.1'
    playService = '15.0.1'
    firebase = '15.0.2'
    viewModel = '1.1.1'
    room = '1.1.0'
    butterKnife = '8.8.1'
    glide = '4.7.1'
    constraintLayout = '1.1.0'
    retrofit = '2.3.0'
    rxAndroid = '2.1.0'
    rxJava = '2.2.2'
    junit = '4.12'
    runner = '1.0.2'
    esspressoCore = '3.0.2'
    commonsio = '2.6'
    multidex = '1.0.3'
    blurkit = '1.0.0'
    logging_interceptor = '3.4.1'
    rebound = '0.3.8'
}

def applications = [project('customerClient'), project('storeClient'), project('supervisorClient')]
configure(applications) {
    apply plugin: 'com.android.application'
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'

    android {
        configurations.all {
            resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        }

        lintOptions {
            disable 'LongLogTag', 'CheckResult'
        }

        defaultConfig {
            multiDexEnabled true

            //blurkit config
            renderscriptTargetApi rootProject.ext.targetSdk
            renderscriptSupportModeEnabled true
        }
    }
    dependencies {
        implementation project(":clientCore")

        implementation "com.android.support:appcompat-v7:$support"
        implementation "com.android.support:design:$support"
        implementation "com.android.support:cardview-v7:$support"
        implementation "com.android.support:recyclerview-v7:$support"
        implementation "com.android.support:preference-v7:$support"

        implementation "android.arch.lifecycle:extensions:$viewModel"
        annotationProcessor "android.arch.lifecycle:common-java8:$viewModel"

        implementation "com.jakewharton:butterknife:$butterKnife"
        annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"

        implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
        implementation "com.android.support:multidex:$multidex"

        implementation "junit:junit:$junit"
        androidTestImplementation "com.android.support.test:runner:$runner"
        androidTestImplementation "com.android.support.test.espresso:espresso-core:$esspressoCore"

        implementation "com.facebook.rebound:rebound:$rebound"

        implementation "com.google.firebase:firebase-messaging:$firebase"
        implementation "com.google.firebase:firebase-core:$firebase"

        implementation "com.google.android.gms:play-services-maps:$playService"
        implementation "com.google.android.gms:play-services-location:$playService"
        implementation "com.google.android.gms:play-services-places:$playService"

        implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
        implementation "com.kakao.sdk:usermgmt:1.7.1"
        implementation 'com.shuhart.stepview:stepview:1.3.1'
        implementation 'com.google.maps.android:android-maps-utils:0.5'
    }
}

def libraries = [project('clientCore')]
configure(libraries) {
    apply plugin: 'com.android.library'

    android {
        lintOptions {
            disable 'LongLogTag', 'CheckResult'
        }

        defaultConfig {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
                }
            }
        }
    }
    dependencies {
        implementation "com.android.support:appcompat-v7:$support"
        implementation "com.android.support:recyclerview-v7:$support"

        implementation "com.squareup.retrofit2:retrofit:$retrofit"
        implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
        implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor"
        implementation "com.squareup.retrofit2:converter-gson:$retrofit"
        implementation "com.squareup.retrofit2:converter-scalars:$retrofit"

        implementation "android.arch.persistence.room:runtime:$room"
        implementation "android.arch.persistence.room:rxjava2:$room"
        annotationProcessor "android.arch.persistence.room:compiler:$room"
        implementation "android.arch.persistence.room:guava:$room"

        api "io.reactivex.rxjava2:rxandroid:$rxAndroid"
        api "io.reactivex.rxjava2:rxjava:$rxJava"

        implementation "junit:junit:$junit"
        androidTestImplementation "com.android.support.test:runner:$runner"
        androidTestImplementation "com.android.support.test.espresso:espresso-core:$esspressoCore"

        implementation "com.google.android.gms:play-services-nearby:$playService"
        implementation "commons-io:commons-io:$commonsio"

        api "com.github.bumptech.glide:glide:$glide"
        annotationProcessor "com.github.bumptech.glide:compiler:$glide"

        api "com.wonderkiln:blurkit:$blurkit"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
