/*
 * Copyright (c) 2018. Mark Soft - All Rights Reserved
 * Unauthorized copying of this file, via any medium is strictly prohibited
 * Proprietary and confidential
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com/' }
        maven { url 'http://devrepo.kakao.com:8088/nexus/content/groups/public/' }
    }
}

ext {
    compileSdk = 28
    targetSdk = 28
    minSdk = 21

}

def applications = [project('customerClient'), project('storeClient'), project('supervisorClient')]
configure(applications) {
    apply plugin: 'com.android.application'
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'

    android {
        configurations.all {
            resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        }

        lintOptions {
            disable 'LongLogTag', 'CheckResult'
            abortOnError false
        }

        defaultConfig {
            multiDexEnabled true

            //blurkit config
            renderscriptTargetApi rootProject.ext.targetSdk
            renderscriptSupportModeEnabled true
        }
    }
    dependencies {
        implementation project(":clientCore")

        implementation "com.android.support:appcompat-v7:28.0.0"
        implementation "com.android.support:design:28.0.0"
        implementation "com.android.support:cardview-v7:28.0.0"
        implementation "com.android.support:recyclerview-v7:28.0.0"
        implementation "com.android.support:preference-v7:28.0.0"

        implementation "android.arch.lifecycle:extensions:1.1.1"
        annotationProcessor "android.arch.lifecycle:common-java8:1.1.1"

        implementation "com.jakewharton:butterknife:8.8.1"
        annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"

        implementation "com.android.support.constraint:constraint-layout:1.1.3"
        implementation "com.android.support:multidex:1.0.3"

        implementation "junit:junit:4.12"
        androidTestImplementation "com.android.support.test:runner:1.0.2"
        androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"

        implementation "com.facebook.rebound:rebound:0.3.8"

        implementation "com.google.firebase:firebase-messaging:17.3.4"
        implementation "com.google.firebase:firebase-core:16.0.6"

        implementation "com.google.android.gms:play-services-maps:16.0.0"
        implementation "com.google.android.gms:play-services-location:16.0.0"
        implementation "com.google.android.gms:play-services-places:16.0.0"

        implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
        implementation "com.kakao.sdk:usermgmt:1.7.1"
        implementation 'com.shuhart.stepview:stepview:1.3.1'
        implementation 'com.google.maps.android:android-maps-utils:0.5'
    }
}

def libraries = [project('clientCore')]
configure(libraries) {
    apply plugin: 'com.android.library'

    android {
        lintOptions {
            disable 'LongLogTag', 'CheckResult'
        }

        defaultConfig {
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
                }
            }
        }
    }
    dependencies {
        implementation "com.android.support:appcompat-v7:28.0.0"
        implementation "com.android.support:recyclerview-v7:28.0.0"

        implementation "com.squareup.retrofit2:retrofit:2.3.0"
        implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
        implementation "com.squareup.retrofit2:converter-gson:2.3.0"
        implementation "com.squareup.retrofit2:converter-scalars:2.3.0"
        implementation "com.squareup.okhttp3:logging-interceptor:3.4.1"

        implementation "android.arch.persistence.room:runtime:1.1.1"
        implementation "android.arch.persistence.room:rxjava2:1.1.1"
        annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
        implementation "android.arch.persistence.room:guava:1.1.1"

        api "io.reactivex.rxjava2:rxandroid:2.1.0"
        api "io.reactivex.rxjava2:rxjava:2.2.2"

        implementation "junit:junit:4.12"
        androidTestImplementation "com.android.support.test:runner:1.0.2"
        androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"

        implementation "com.google.android.gms:play-services-nearby:16.0.0"
        implementation "commons-io:commons-io:2.6"

        api "com.github.bumptech.glide:glide:4.7.1"
        annotationProcessor "com.github.bumptech.glide:compiler:4.7.1"

        api "com.wonderkiln:blurkit:1.0.0"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
